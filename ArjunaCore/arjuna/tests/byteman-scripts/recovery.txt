########################################################################
#
# byteman script used to ensure that tests can synchronize with various
# actions performed by the recovery code

#########################################################################
# rules to identify progress of the periodic recovery listener thread
#

# if a test has set up a rendezvous under key "PR listener run" then
# flag the listener so that it enters the rendezvous when it starts runnning

RULE periodic recovery set up listener for rendezvous
CLASS com.arjuna.ats.internal.arjuna.recovery.PeriodicRecovery
METHOD <init>
AT CALL  Listener.start
BIND pr : PeriodicRecovery = $0
IF isRendezvous("PR listener run", 2)
DO debug("flagging PR listener"),
   flag(pr.getListener())
ENDRULE

# if the listener has been flagged then make it enter
# the PR run rendezvous just before calling accept

RULE listener rendezvous at run
CLASS com.arjuna.ats.internal.arjuna.recovery.Listener
METHOD run()
AT ENTRY
BIND listener : Listener = $0
IF flagged(listener) && isRendezvous("PR listener run", 2)
DO debug("listener rendezvous at PR listener run"),
   rendezvous("PR listener run")
ENDRULE

#########################################################################
#
# rules appropriate to specific tests

#########################################################################
# RecoveryManagerStartStopTest wants to rendezvous at the point where
# the periodic recovery worker service has started running
#

# set up the PR listener rendezvous and add a rendezvous
# for the Connection class to enter once it is started

RULE RecoveryManagerStartStopTest create rendezvous
CLASS com.hp.mwtests.ts.arjuna.recovery.RecoveryManagerStartStopTest
METHOD testStartStop()
AT ENTRY
BIND NOTHING
IF TRUE
DO debug("create rendezvous for PR listener run"),
   createRendezvous("PR listener run", 2),
   createRendezvous("RecoveryManagerStartStopTest Connection run", 3),
   createJoin("Listener Connection", 2)
ENDRULE

# rendezvous with the PR listener to make sure it has started before
# any listener clients are added

RULE RecoveryManagerStartStopTest rendezvous before adding clients
CLASS com.hp.mwtests.ts.arjuna.recovery.RecoveryManagerStartStopTest
METHOD testStartStop()
AT CALL addRecoveryClient
BIND NOTHING
IF TRUE
DO debug("RecoveryManagerStartStopTest rendezvous at PR listener run"),
   rendezvous("PR listener run")
ENDRULE

# make the connection threads rendezvous with the test thread

RULE RecoveryManagerStartStopTest connection rendezvous at run
CLASS com.arjuna.ats.internal.arjuna.recovery.Connection
METHOD run()
AT CALL doWork
BIND NOTHING
IF isRendezvous("RecoveryManagerStartStopTest Connection run", 3)
DO debug("connection rendezvous at doWork"),
   rendezvous("RecoveryManagerStartStopTest Connection run")
ENDRULE

# make the test threads rendezvous with the connection thread
# this ensures the shutdown does not happen until the connection threads
# have started running

RULE RecoveryManagerStartStopTest rendezvous after client connections started
CLASS com.hp.mwtests.ts.arjuna.recovery.RecoveryManagerStartStopTest
METHOD testStartStop()
AFTER CALL addRecoveryClient 2
BIND NOTHING
IF TRUE
DO debug("RecoveryManagerStartStopTest rendezvous after 2nd addRecoveryClient"),
   rendezvous("RecoveryManagerStartStopTest Connection run")
ENDRULE

# The listener code gets notified that its connection thread is exiting but does not
# bother to join the thread at shutdown since it would also be forced to join all other
# threads during normal operation. it avoids doing this because it is waiting in an
# accept and so can only be woken via an interrupt -- a pointless waste of CPU. however for
# testing we want to be sure that the connection thread has actually exited. so we
# use a joiner to join the connection threads from the listener thread.
# the connection threads register themselves when they run the listener
# callback by calling the helper method joinEnlist(key). the listener thread joins
# N enlisted threads by calling the helper method joinWait(key, N). The Object
# supplied as key must match.

RULE listener join wait
CLASS com.arjuna.ats.internal.arjuna.recovery.Listener
METHOD run()
AT RETURN
BIND NOTHING
IF isJoin("Listener Connection", 2)
DO debug("Listener Connection calling joinWait 2 " + Thread.currentThread()),
   joinWait("Listener Connection", 2),
   debug("Listener Connection called joinWait 2 " + Thread.currentThread())
ENDRULE

RULE connection join enlist
CLASS com.arjuna.ats.internal.arjuna.recovery.Listener
METHOD removeConnection
AT RETURN
BIND NOTHING
IF joinEnlist("Listener Connection")
DO debug("Listener Connection joinEnlist " + Thread.currentThread())
ENDRULE
