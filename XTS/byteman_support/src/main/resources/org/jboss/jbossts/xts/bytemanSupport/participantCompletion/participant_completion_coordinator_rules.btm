RULE create counter
CLASS org.jboss.jbossts.xts.bytemanSupport.participantCompletion.ParticipantCompletionCoordinatorRules
METHOD setParticipantCount(Integer)
BIND counter:Integer=$1, disabled:boolean=false
IF TRUE
DO debug("participant_completion.counter.create: " + counter),
  deleteCounter("completes"),
  createCounter("completes", counter),
  flag("enabled")
ENDRULE

RULE complete called
CLASS com.arjuna.webservices11.wsba.sei.BusinessAgreementWithParticipantCompletionCoordinatorPortTypeImpl
METHOD completedOperation(NotificationType)
AT RETURN
BIND NOTHING
IF (flagged("enabled") AND debug("participant_completion.counter.decrement") AND decrementCounter("completes") == 0)
DO debug("participant_completion.called.waking"),
   signalWake("complete", true),
   debug("participant_completion.called.donewake")
ENDRULE

RULE close called
CLASS com.arjuna.webservices11.wsarjtx.sei.TerminationCoordinatorPortTypeImpl
METHOD closeOperation(NotificationType)
BIND NOTHING
IF (flagged("enabled"))
DO debug("participant_completion.close.waiting"),
   waitFor("complete"),
   debug("participant_completion.close.woken"),
   clear("enabled")
ENDRULE

RULE cancel called
CLASS com.arjuna.webservices11.wsarjtx.sei.TerminationCoordinatorPortTypeImpl
METHOD cancelOperation(NotificationType)
BIND NOTHING
IF (flagged("enabled"))
DO debug("participant_completion.cancel.waiting"),
   waitFor("complete"),
   debug("participant_completion.cancel.woken"),
   clear("enabled")
ENDRULE